<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	creationComplete="executa_listar_clientes(),executa_listar_cidades()">
	<mx:DataGrid x="44.5" y="332" change="cliente_selecionado(event)" width="785" id="grid_clientes" height="157" dataProvider="{clientes_array}">
		<mx:columns>
			<mx:DataGridColumn headerText="Codigo" dataField="codigo"/>
			<mx:DataGridColumn headerText="Nome" dataField="nome"/>
			<mx:DataGridColumn headerText="Email" dataField="email"/>
            <mx:DataGridColumn headerText="foto" dataField="foto"/>
            <mx:DataGridColumn headerText="Fone" dataField="fone"/>
            <mx:DataGridColumn headerText="Codcid" dataField="codcid"/>
            <mx:DataGridColumn headerText="Nome Cidade" dataField="nomecidade"/>                          			
		</mx:columns>
	</mx:DataGrid>
	<mx:Label x="81" y="10" text="Manutenção do cadastro de Clientes" color="#F91F09" fontSize="20"/>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.CloseEvent;
					
			import mx.rpc.events.ResultEvent;
			import mx.collections.ArrayCollection;
			import mx.binding.utils.BindingUtils;
			import br.com.informaticon.dto.dadosClientesDTO;
			import br.com.informaticon.RemoteObject.ro_clientes;
			
			//NERI NEITZKE - VIDEOAULAS WWW.INFORMATICON.COM.BR
			private var dados_clientes:dadosClientesDTO;
			private var remoteobject_cliente:ro_clientes = ro_clientes.getInstance();
			private var tipopesquisa:String = "tem"; 
			
			[Bindable]
			public var clientes_array:ArrayCollection;
			[Bindable]
			public var cidades_array:ArrayCollection;
			
			//**************** busca clientes
			public function executa_listar_clientes():void
			{
				remoteobject_cliente.listar_clientes(ti_pesquisa.text,tipopesquisa, result_lista_clientes);
			}
			public function result_lista_clientes(event:ResultEvent):void
			{
				clientes_array = new ArrayCollection(event.result as Array);
			}
			//**********************
			public function executa_listar_cidades():void
			{
				remoteobject_cliente.listar_cidades(result_lista_cidades);
			}
			
			public function result_lista_cidades(event:ResultEvent):void
			{
				cidades_array = new ArrayCollection(event.result as Array);
			}
			
			//busca codigo da cidade
			public function retorna_cod_cidade():void
			{
				remoteobject_cliente.busca_codigo_cidade(combo_cidade.text,resultCodCidade);
			}
			
			public function resultCodCidade(event:ResultEvent):void
			{
				ti_codcid.text = event.result.toString();
			}
			

			//busca codigo da cidade
			public function retorna_nome_cidade():void
			{
				remoteobject_cliente.busca_nome_cidade(Number(ti_codcid.text),resultNomeCidade);
			}
			
			public function resultNomeCidade(event:ResultEvent):void
			{
				combo_cidade.text = event.result.toString();
			}
			
			public function cliente_selecionado(event:Event):void
			{
				dados_clientes = dadosClientesDTO(event.currentTarget.selectedItem);
				ti_codigo.text = dados_clientes.codigo.toString();
				ti_nome.text   = dados_clientes.nome;
                ti_email.text  = dados_clientes.email;
                ti_fone.text   = dados_clientes.fone;
                ti_foto.text   = dados_clientes.foto;
                ti_codcid.text  = dados_clientes.codcid.toString();
                combo_cidade.text = dados_clientes.nomecidade;	
                botao_excluir.enabled = true;
                						
			}
			
			public function limpar_campos():void
			{
				ti_codigo.text = "";
				ti_nome.text = "";
				ti_email.text = "";
				ti_fone.text = "";
				ti_foto.text = "";
				ti_codcid.text = "";
				botao_excluir.enabled = false;
				
			}
			public function gravar_cliente():void
			{
				var dados_cliente_gravar:dadosClientesDTO = new dadosClientesDTO();
				dados_cliente_gravar.codigo = Number(ti_codigo.text);
				dados_cliente_gravar.nome   = ti_nome.text;
				dados_cliente_gravar.email  = ti_email.text;
				dados_cliente_gravar.fone   = ti_fone.text;
				dados_cliente_gravar.foto   = ti_foto.text;
				dados_cliente_gravar.codcid = Number(ti_codcid.text);
				dados_cliente_gravar.nomecidade = combo_cidade.text;				

				if(ti_codigo.text != '')
				   remoteobject_cliente.gravar_cliente(dados_cliente_gravar,resultAlteracaoCliente);
				else   			
				   remoteobject_cliente.gravar_cliente(dados_cliente_gravar,resultGravacaoCliente);
			}
			
			public function resultGravacaoCliente(event:ResultEvent):void
			{
				clientes_array.addItem(event.result);
			}
			public function resultAlteracaoCliente(event:ResultEvent):void
			{
				clientes_array.setItemAt(event.result,grid_clientes.selectedIndex);
			}
			
			public function excluir_dados_cliente():void
            {
            	if (Number(ti_codigo.text) > 0)
            	{
      				Alert.yesLabel="Sim";
				    Alert.noLabel ="Não";
            	    Alert.show('Voce tem certeza que deja excluir o cliente: '+ti_nome.text,'Excluindo Cliente',3,this, confirmar_exclusao)
            	}             	    
            	else
            	    Alert.show("Voce deve selecionar um registro para poder exlcluir");    
            }
            
            private function confirmar_exclusao(event:CloseEvent):void{
				if(event.detail == Alert.YES)
				{
				  dados_clientes = grid_clientes.selectedItem as dadosClientesDTO;
				  remoteobject_cliente.excluir_cliente(dados_clientes,resultExclusaoCliente);
				}
				else
				  Alert.show("O cliente nao foi excluido");  
			} 
			
			public function resultExclusaoCliente(event:ResultEvent):void
			{
				clientes_array.removeItemAt(grid_clientes.selectedIndex);
			}
			
			//envio da foto e manipulação
			private var arquivo:FileReference = null;
			public function localizar_arquivo_documentos():void
			{
				arquivo = new FileReference();
				arquivo.addEventListener(Event.SELECT, quando_selecionado);
				arquivo.addEventListener(Event.COMPLETE, quando_enviado);
				var tipo_arquivos:FileFilter = new FileFilter("Arquivos do tipo Documentos","*.doc; *.txt");
				var array_tipos_arquivos:Array = new Array(tipo_arquivos);
				arquivo.browse(array_tipos_arquivos);
			}
			
			public function localizar_arquivo_imagens():void
			{
				arquivo = new FileReference();
				arquivo.addEventListener(Event.SELECT, quando_selecionado);
				arquivo.addEventListener(Event.COMPLETE, quando_enviado);
				var tipo_arquivos:FileFilter = new FileFilter("Arquivos do tipo Imagens","*.jpg; *.gif");
				var array_tipos_arquivos:Array = new Array(tipo_arquivos);
				arquivo.browse(array_tipos_arquivos);
			}
			
			public function quando_selecionado(event:Event):void
			{
				ti_foto.text = arquivo.name;
			}
			public function quando_enviado(event:Event):void
			{
				Alert.show("Arquivo enviado com sucesso!");
				ti_foto.text = "";
				ti_foto.text = arquivo.name;
			}	
			public function enviar_arquivo():void
			{	if (arquivo == null) return;			
				var url_envio:String = "http://localhost/flex_php/neri_crud_amfphp_dto/bin-debug/upload.php";
				var request:URLRequest = new URLRequest(url_envio);
				request.method = URLRequestMethod.POST;
				arquivo.upload(request,"nome_do_arquivo");
			}
		]]>
	</mx:Script>	
	<mx:Label x="44.5" y="188" text="Codigo.:"/>
	<mx:Label x="186.5" y="188" text="Nome.:"/>
	<mx:Label x="44.5" y="216" text="E-mail:"/>
	<mx:Label x="283.5" y="244" text="Codigo Cidade:"/>
	<mx:Label x="44.5" y="244" text="Telefone:"/>
	<mx:TextInput x="105.5" y="186" width="60" id="ti_codigo"/>
	<mx:TextInput x="247.5" y="186" width="254" id="ti_nome"/>
	<mx:TextInput x="105.5" y="214" width="254" id="ti_email"/>
	<mx:TextInput x="105.5" y="242" id="ti_fone"/>
	<mx:TextInput x="383.5" y="242" width="47" id="ti_codcid" focusOut="retorna_nome_cidade()"/>
	<mx:Button x="167.5" y="302" label="Gravar" click="gravar_cliente()"/>
	<mx:Button x="44" y="302" label="Novo Registro" click="limpar_campos()"/>
	<mx:Button x="240" y="302" label="Excluir" click="excluir_dados_cliente()" id="botao_excluir" enabled="false"/>
	<mx:ComboBox change="retorna_cod_cidade()" dataProvider="{cidades_array}" labelField="nome" x="438.5" y="242" width="197" id="combo_cidade"></mx:ComboBox>
	<mx:Panel x="660" y="138" width="224" height="178" layout="absolute" title="Foto do Cliente">
		<mx:Image x="0" y="0" width="125" height="104" id="foto_cliente" source="{ti_foto.text}"/>
		<mx:Button click="localizar_arquivo_imagens()" x="143" y="49" label="..." width="61" height="28"/>
		<mx:Button x="142" y="85" label="Enviar" click="enviar_arquivo()"/>
		<mx:TextInput x="22" y="112" id="ti_foto"/>
	</mx:Panel>
	<mx:Panel x="27" y="49" width="714" height="81" layout="absolute" title="Pesquisa">
		<mx:TextInput x="10" y="10" width="225" id="ti_pesquisa"/>
		<mx:Button x="253" y="10" label="pesquisar" click="executa_listar_clientes()"/>
		<mx:RadioButton x="339" y="10" label="Tem" width="56" selected="true" click="tipopesquisa = 'tem'"/>
		<mx:RadioButton x="403" y="10" label="Inicia com" width="87" click="tipopesquisa = 'iniciacom'"/>
		<mx:RadioButton x="498" y="10" label="Finaliza" width="73" click="tipopesquisa = 'finalizacom'"/>
	</mx:Panel>
</mx:Application>














